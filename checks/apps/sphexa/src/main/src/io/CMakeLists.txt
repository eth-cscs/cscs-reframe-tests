
function(enableH5hut exename)
    if(SPH_EXA_WITH_H5HUT)
        target_compile_definitions(${exename} PUBLIC SPH_EXA_HAVE_H5HUT)
        target_include_directories(${exename} PUBLIC ${H5hut_INCLUDE_DIR})
        if(SPH_EXA_HDF5_PARALLEL_MODE STREQUAL "independent")
          target_compile_definitions(${exename} PRIVATE  SPH_EXA_HDF5_PARALLEL_MODE_INDEPENDENT)
        elseif(SPH_EXA_HDF5_PARALLEL_MODE STREQUAL "collective")
            target_compile_definitions(${exename} PRIVATE SPH_EXA_HDF5_PARALLEL_MODE_COLLECTIVE)
        endif()
        # necessary because the HDF5_INCLUDE_DIRS is empty on CRAY and HIP-compilation is done without the cray-wrapper
        target_include_directories(${exename} PUBLIC $ENV{HDF5_DIR}/include)
        target_link_libraries(${exename} PRIVATE H5hut ${HDF5_LIBRARIES})
    endif()
endfunction()

add_library(io id_tag_utils.cpp ifile_io_ascii.cpp ifile_io_hdf5.cpp arg_parser.cpp)
target_include_directories(io PRIVATE ${CSTONE_DIR} ${SPH_EXA_INCLUDE_DIRS} ${MPI_CXX_INCLUDE_PATH})
target_link_libraries(io PRIVATE ${MPI_CXX_LIBRARIES})

if (CMAKE_CUDA_COMPILER OR CMAKE_HIP_COMPILER)
    add_library(io_gpu OBJECT id_tag_utils.cu)
    target_include_directories(io_gpu PRIVATE ${SPH_EXA_INCLUDE_DIRS})
    if (CMAKE_CUDA_COMPILER)
        target_link_libraries(io_gpu PRIVATE CUDA::cudart)
    endif()
    if (CMAKE_HIP_COMPILER)
        target_link_libraries(io_gpu PRIVATE hip::host)
    endif()
endif()

enableH5hut(io)
